#target name
target_name = 'cordic_unit_test'

#implicit dependencies
config_files = ['../../config/sdr_config.xml','stimulus.xml']
#source files
source_files = 'test_bench.cpp'

#custom build flags
flags = ['-Wall', '-W', '-DTIXML_USE_TICPP']

#setup libraries to link and paths
libs=['tinyxmlcpp', 'systemc']
lib_path=['/usr/lib','/usr/local/lib']

#define build environment
env = Environment(LIBS=libs, LIBPATH=lib_path, CXXFLAGS=flags, 
      CPPPATH=['/usr/local/include/sdr_simulator','./'] )

code_gen = env.Program( target='code_generator', source='generate_header.cpp')
env.Depends( code_gen, config_files )

generate_header = env.Command('test_bench.hpp', code_gen, './code_generator')
env.Depends( generate_header, config_files )

prog1 = env.Program( target=target_name , source=source_files )
env.Depends( prog1, generate_header )

env.Clean('.', env.Glob('*.dat'))

# code generator
#g++ -o codeGen generate_header.cpp -DTIXML_USE_TICPP -ltinyxmlcpp
